import org.motechproject.tama.healthtips.domain.HealthTipParams;
import java.util.Arrays;
import java.util.List;

global org.motechproject.tama.healthtips.service.HealthTipRuleService.HealthTipList  healthTips;

rule "Less than 1 month into ART"
    when
        HealthTipParams(numberOfMonthsSinceTreatmentStarted < 1)
    then
        for (String file : Arrays.asList("HT001a", "HT002a", "HT003a", "HT004a", "HT005a","HT011a", "HT012a", "HT013a", "HT017a", "HT018a")) {
            healthTips.addHealthTip(file, "2");
        }

        for (String file : Arrays.asList("HT033a", "HT034a", "HT035a", "HT036a", "HT037a", "HT038a", "HT039a", "HT040a")) {
            healthTips.addHealthTip(file, "3");
        }
end

rule "Less than 1 month into ART and patient on daily pill reminder"
    when
        HealthTipParams(numberOfMonthsSinceTreatmentStarted < 1)
        HealthTipParams(patientOnDailyPillReminder == "true")
    then
        for (String file : Arrays.asList("HT014a", "HT019a", "HT021a")) {
            healthTips.addHealthTip(file, "2");
        }
end

rule "Less than 1 month into ART and patient not on daily pill reminder"
    when
        HealthTipParams(numberOfMonthsSinceTreatmentStarted < 1)
        HealthTipParams(patientOnDailyPillReminder == "false")
    then
        for (String file : Arrays.asList("HT015a", "HT020a", "HT022a")) {
            healthTips.addHealthTip(file, "2");
        }
end

rule "Dosage Missed Last Week"
    when
        HealthTipParams(dosageMissedLastWeek == "true")
    then
        for (String file : Arrays.asList("HT004a", "HT002a", "HT011a", "HT012a")) {
            healthTips.addHealthTip(file, "1");
        }
end

rule "Dosage Taken Late Last Week and user on daily reminder"
    when
        HealthTipParams(anyDoseTakenLateLastWeek == "true")
        HealthTipParams(patientOnDailyPillReminder == "true")
    then
        for (String file : Arrays.asList("HT013a", "HT018a")) {
            healthTips.addHealthTip(file, "1");
        }
end

rule "Dosage Missed Last Week and user on daily reminder"
    when
        HealthTipParams(dosageMissedLastWeek == "true")
        HealthTipParams(patientOnDailyPillReminder == "true")
    then
        for (String file : Arrays.asList("HT021a", "HT014a")) {
            healthTips.addHealthTip(file, "1");
        }
end

rule "Dosage Missed Last Week and user on four day recall"
    when
        HealthTipParams(dosageMissedLastWeek == "true")
        HealthTipParams(patientOnDailyPillReminder == "false")
    then
        for (String file : Arrays.asList("HT022a", "HT015a")) {
            healthTips.addHealthTip(file, "1");
        }
end

rule "Less than 2 months, but more than 1 month into ART and patient on daily pill reminder"
    when
        HealthTipParams(numberOfMonthsSinceTreatmentStarted >= 1)
        HealthTipParams(numberOfMonthsSinceTreatmentStarted < 2)
        HealthTipParams(patientOnDailyPillReminder == "true")
    then
        for (String file : Arrays.asList("HT006a" , "HT007a" , "HT008a" , "HT009a")) {
            healthTips.addHealthTip(file, "2");
        }

        for (String file : Arrays.asList("HT001a", "HT002a", "HT003a", "HT004a", "HT005a",
                                         "HT011a", "HT012a", "HT013a", "HT017a", "HT018a",
                                         "HT033a", "HT034a", "HT035a", "HT036a",
                                         "HT037a", "HT038a", "HT039a", "HT040a")) {
            healthTips.addHealthTip(file, "3");
        }

        for (String file : Arrays.asList("HT014a", "HT019a", "HT021a")) {
            healthTips.addHealthTip(file, "3");
        }
end

rule "Less than 2 months, but more than 1 month into ART and patient not on daily pill reminder"
    when
        HealthTipParams(numberOfWeeksSinceTreatmentStarted < 8)
        HealthTipParams(numberOfWeeksSinceTreatmentStarted > 4)
        HealthTipParams(patientOnDailyPillReminder == "false")
    then
        for (String file : Arrays.asList("HT006a" , "HT007a" , "HT008a" , "HT009a")) {
            healthTips.addHealthTip(file, "2");
        }
        for (String file : Arrays.asList("HT001a", "HT002a", "HT003a", "HT004a", "HT005a",
                                         "HT011a", "HT012a", "HT013a", "HT017a", "HT018a",
                                         "HT033a", "HT034a", "HT035a", "HT036a",
                                         "HT037a", "HT038a", "HT039a", "HT040a")) {
            healthTips.addHealthTip(file, "3");
        }
        for (String file : Arrays.asList("HT015a", "HT020a", "HT022a")) {
            healthTips.addHealthTip(file, "3");
        }
end

rule "Greater than 2 months into ART"
    when
        HealthTipParams(numberOfMonthsSinceTreatmentStarted >= 2)
    then
        for (String file : Arrays.asList("HT001a", "HT002a", "HT003a", "HT004a", "HT005a",
                                         "HT006a", "HT007a", "HT008a", "HT009a", "HT011a",
                                         "HT012a", "HT013a", "HT017a", "HT018a",
                                         "HT033a", "HT034a", "HT035a", "HT036a",
                                         "HT037a", "HT038a", "HT039a", "HT040a")) {
            healthTips.addHealthTip(file, "3");
        }
end

rule "Greater than 2 months into ART and patient on Daily Pill Reminder"
   when
        HealthTipParams(numberOfMonthsSinceTreatmentStarted >= 2)
        HealthTipParams(patientOnDailyPillReminder == "true")
   then
        for (String file : Arrays.asList("HT021a", "HT014a", "HT019a")) {
            healthTips.addHealthTip(file, "3");
        }
end

rule "Greater than 2 months into ART and patient not on Daily Pill Reminder"
   when
        HealthTipParams(numberOfMonthsSinceTreatmentStarted >= 2)
        HealthTipParams(patientOnDailyPillReminder == "false")
   then
        for (String file : Arrays.asList("HT022a", "HT015a", "HT020a")) {
            healthTips.addHealthTip(file, "3");
        }
end

rule "Less than 12 months but more than 2 months into ART and date of last CD4 test more than two and half months ago"
    when
        HealthTipParams(numberOfMonthsSinceTreatmentStarted >= 2)
        HealthTipParams(numberOfMonthsSinceTreatmentStarted < 12)
        HealthTipParams(numberOfWeeksSinceLastCD4LabTest > 10)
    then
        for (String file : Arrays.asList("HT008a", "HT009a")) {
            healthTips.addHealthTip(file, "1");
        }
end

rule "Less than 12 months but more than 2 months into ART and date of last CD4 test less than two and half months ago"
    when
        HealthTipParams(numberOfMonthsSinceTreatmentStarted >= 2)
        HealthTipParams(numberOfMonthsSinceTreatmentStarted < 12)
        HealthTipParams(numberOfWeeksSinceLastCD4LabTest < 10)
    then
        for (String file : Arrays.asList("HT008a", "HT009a")) {
            healthTips.addHealthTip(file, "3");
        }
end

rule "Greater than 12 months into ART, last CD4 count < 350 and date of last CD4 test more than two and half months ago but less than five and a half months ago"
    when
        HealthTipParams(numberOfMonthsSinceTreatmentStarted >= 12)
        HealthTipParams(lastCD4Count < 350)
        HealthTipParams(numberOfWeeksSinceLastCD4LabTest < 22)
        HealthTipParams(numberOfWeeksSinceLastCD4LabTest > 10)
    then
        for (String file : Arrays.asList("HT010a")) {
            healthTips.addHealthTip(file, "1");
        }
end
    
rule "Greater than 12 months into ART, last CD4 count < 350 and date of last CD4 test less than two and half months ago"
    when
        HealthTipParams(numberOfMonthsSinceTreatmentStarted >= 12)
        HealthTipParams(lastCD4Count < 350)
        HealthTipParams(numberOfWeeksSinceLastCD4LabTest < 10)
    then
        for (String file : Arrays.asList("HT010a")) {
            healthTips.addHealthTip(file, "3");
        }
end

rule "Greater than 12 months into ART, last CD4 count > 350 and date of last CD4 test more than five and a half months ago"
    when
        HealthTipParams(numberOfMonthsSinceTreatmentStarted >= 12)
        HealthTipParams(lastCD4Count > 350)
        HealthTipParams(numberOfWeeksSinceLastCD4LabTest > 22)
    then
        for (String file : Arrays.asList("HT010a")) {
            healthTips.addHealthTip(file, "1");
        }
end

rule "Greater than 12 months into ART, last CD4 count > 350 and date of last CD4 test less than less than five and a half months ago but more than two and a half months ago"
    when
        HealthTipParams(numberOfMonthsSinceTreatmentStarted >= 12)
        HealthTipParams(lastCD4Count > 350)
        HealthTipParams(numberOfWeeksSinceLastCD4LabTest < 22)
        HealthTipParams(numberOfWeeksSinceLastCD4LabTest > 10)
    then
        for (String file : Arrays.asList("HT010a")) {
            healthTips.addHealthTip(file, "3");
        }
end